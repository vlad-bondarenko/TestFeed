plugins {
    id 'groovy'
    id 'org.springframework.boot' version '3.3.0'
    id 'io.spring.dependency-management' version '1.1.5'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

sourceSets {
    integration {
        groovy.srcDir "$projectDir/src/integration/groovy"
        resources.srcDir "$projectDir/src/integration/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

repositories {
    mavenCentral()
}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}

dependencies {
    // Spring implementation dependencies
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.apache.groovy:groovy'

    testAndDevelopmentOnly 'org.springframework.boot:spring-boot-docker-compose'

    // Test implementation
    testImplementation(
            'org.springframework.boot:spring-boot-starter-test',

            'org.spockframework:spock-core:2.4-M4-groovy-4.0',
            'org.spockframework:spock-spring:2.4-M4-groovy-4.0'
    )
    // Test run time only
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed"
    }
}

tasks.register('integrationTest', Test) {
    description = "Run integration tests"
    group = "verification"
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
}

check.dependsOn integrationTest

integrationTest {
    useJUnitPlatform()

    testLogging {
        events "passed"
    }
}